// https://graphviz.org/doc/info/lang.html

@top Graph {
  kwstrict? (kwgraph | kwdigraph) ID? "{" stmt_list "}"
}

stmt_list {
  (stmt ";"?)*
}

stmt[@isGroup=Stmt] {
    NodeStmt
  | EdgeStmt
  | AttrStmt
  | Prop { ID "=" ID }
  | Subgraph
}

AttrStmt {
  (kwgraph | kwnode | kwedge) attr_list
}

attr_list {
  ("[" a_list? "]")+
}

a_list {
  ( Attr { ID "=" ID } (";" | ",")?)+
}

EdgeStmt {
  (node_id | Subgraph) edgeRHS attr_list?
}

edgeRHS {
  (edgeop (node_id | Subgraph))+
}

NodeStmt {
  node_id attr_list?
}

node_id {
  ID port?
}

port {
    ":" ID (":" CompassPt)?
  | ":" CompassPt
}

CompassPt {
  @extend<Simpleid, "n" | "ne" | "e" | "se" | "s" | "sw" | "w" | "nw" | "c" | "_">
}

Subgraph {
  (kwsubgraph ID?)? "{" stmt_list "}"
}

ID {
    Simpleid
  | Numeral
  | Quoted
  | Htmlstr
}

@precedence {
  kwstrict,
  kwgraph,
  kwdigraph,
  kwedge,
  kwnode,
  kwsubgraph,
  Simpleid
}

kwstrict[@name=strict] { @specialize<Simpleid, "strict"> }
kwgraph[@name=graph] { @specialize<Simpleid, "graph"> }
kwdigraph[@name=digraph] { @specialize<Simpleid, "digraph"> }
kwedge[@name=edge] { @specialize<Simpleid, "edge"> }
kwnode[@name=node] { @specialize<Simpleid, "node"> }
kwsubgraph[@name=subgraph] { @specialize<Simpleid, "subgraph"> }

@skip { spaces | newline | LineComment | BlockComment | SharpComment}

@tokens {
  spaces[@export] { $[\u0009 \u000b\u00a0\u1680\u2000-\u200a\u202f\u205f\u3000\ufeff]+ }
  newline[@export] { $[\r\n\u2028\u2029] }

  LineComment { "//" ![\n]* }

  // FIXME line beggining.
  SharpComment { "#" ![\n]* }

  BlockComment { "/*" blockCommentRest }
  blockCommentRest { ![*] blockCommentRest | "*" blockCommentAfterStar }
  blockCommentAfterStar { "/" | "*" blockCommentAfterStar | ![/*] blockCommentRest }

  alphabetic { $[a-zA-Z\u{200}-\u{377}] }
  underscores { "_" }
  digits { $[0-9] }

  Simpleid { (alphabetic | underscores) (alphabetic | underscores | digits)* }

  Numeral { "-"? ("." digits+ | digits+ ("." digits*)?) }

  Quoted { '"' (![\\\n"] | "\\" _)* '"' }

  Htmlstr { '<' (![>])* '>' }

  edgeop[@name=edgeop] { "->" | "--" }

  "{" "}" "[" "]" "," ";" "="
}
