// https://graphviz.org/doc/info/lang.html

@top Graph {
  kwstrict? (kwgraph | kwdigraph) ID? StmtList
}

StmtList {
  "{" (stmt ";"?)* "}"
}

stmt[@isGroup=Stmt] {
    NodeStmt
  | EdgeStmt
  | AttrStmt
  | Prop { AttrName "=" AttrVal }
  | Subgraph
}

AttrStmt {
  (kwgraph | kwnode | kwedge) AttrList
}

AttrList {
  ("[" a_list? "]")+
}

a_list {
  ( Attr { AttrName "=" AttrVal } (";" | ",")?)+
}

EdgeStmt {
  (NodeId | Subgraph) edgeRHS AttrList?
}

edgeRHS {
  (edgeop (NodeId | Subgraph))+
}

NodeStmt {
  NodeId AttrList?
}

NodeId {
  ID port?
}

port {
    ":" ID (":" CompassPt)?
  | ":" CompassPt
}

CompassPt {
  @extend<Simpleid, "n" | "ne" | "e" | "se" | "s" | "sw" | "w" | "nw" | "c" | "_">
}

Subgraph {
  (kwsubgraph ID?)? StmtList
}

ID {
    Simpleid
  | Numeral
  | Quoted
  | Htmlstr
}

AttrName { ID }
AttrVal { ID }

@precedence {
  kwstrict,
  kwgraph,
  kwdigraph,
  kwedge,
  kwnode,
  kwsubgraph,
  Simpleid
}

kwstrict[@name=strict] { @specialize<Simpleid, "strict"> }
kwgraph[@name=graph] { @specialize<Simpleid, "graph"> }
kwdigraph[@name=digraph] { @specialize<Simpleid, "digraph"> }
kwedge[@name=edge] { @specialize<Simpleid, "edge"> }
kwnode[@name=node] { @specialize<Simpleid, "node"> }
kwsubgraph[@name=subgraph] { @specialize<Simpleid, "subgraph"> }

@skip { spaces | LineComment | BlockComment | SharpComment}

@tokens {
  // https://gitlab.com/graphviz/graphviz/-/blob/df53fb2b60cdd06ad6c5f67167d7aa80b3f0fd7c/lib/cgraph/scan.l#L207
  spaces[@export] { $[ \t\r\n]+ }

  LineComment { "//" ![\n]* }

  // FIXME line beggining.
  SharpComment { "#" ![\n]* }

  BlockComment { "/*" blockCommentRest }
  blockCommentRest { ![*] blockCommentRest | "*" blockCommentAfterStar }
  blockCommentAfterStar { "/" | "*" blockCommentAfterStar | ![/*] blockCommentRest }

  alphabetic { $[a-zA-Z\u{200}-\u{377}] }
  underscores { "_" }
  digits { $[0-9] }

  Simpleid { (alphabetic | underscores) (alphabetic | underscores | digits)* }

  Numeral { "-"? ("." digits+ | digits+ ("." digits*)?) }

  Quoted { '"' (![\\\n"] | "\\" _)* '"' }

  Htmlstr { '<' (![>])* '>' }

  edgeop[@name=edgeop] { "->" | "--" }

  "{" "}" "[" "]" "," ";" "="
}
